global proc UnityMeshSync_GUI()
{
    global string $Window = "UnityMeshSync";
    if (`window -exists $Window`) deleteUI -window $Window;
    window -t ("Unity Mesh Sync") -wh 300 660 $Window;

    string $version         = `UnityMeshSync_Settings -q -pluginVersion`;
    string $address         = `UnityMeshSync_Settings -q -address`;
    int    $port            = `UnityMeshSync_Settings -q -port`;
    float  $scaleFactor     = `UnityMeshSync_Settings -q -scaleFactor`;
    int    $removeNamespace = `UnityMeshSync_Settings -q -removeNamespace`;
    int    $syncMeshes      = `UnityMeshSync_Settings -q -syncMeshes`;
    int    $syncNormals     = `UnityMeshSync_Settings -q -syncNormals`;
    int    $syncUVs         = `UnityMeshSync_Settings -q -syncUVs`;
    int    $syncColors      = `UnityMeshSync_Settings -q -syncColors`;
    int    $makeDoubleSided = `UnityMeshSync_Settings -q -makeDoubleSided`;
    int    $bakeDeformers   = `UnityMeshSync_Settings -q -bakeDeformers`;
    int    $syncBlendShapes = `UnityMeshSync_Settings -q -syncBlendShapes`;
    int    $syncBones       = `UnityMeshSync_Settings -q -syncBones`;
    int    $syncTextures    = `UnityMeshSync_Settings -q -syncTextures`;
    int    $syncCameras     = `UnityMeshSync_Settings -q -syncCameras`;
    int    $syncLights      = `UnityMeshSync_Settings -q -syncLights`;
    int    $autosync        = `UnityMeshSync_Settings -q -autosync`;
    int    $animationTS     = `UnityMeshSync_Settings -q -animationTS`;
    int    $animationSPS    = `UnityMeshSync_Settings -q -animationSPS`;
    int    $kfReduction     = `UnityMeshSync_Settings -q -keyframeReduction`;
    int    $keepFlatCurves  = `UnityMeshSync_Settings -q -keepFlatCurves`;

    columnLayout -columnOffset "left" 5;
    rowLayout -numberOfColumns 3;
    text -label "Server:Port";
    textField -text $address -w 120 -cc "umsCBServerAddress" umsCtrlAddress;
    intField -value $port -w 60  -cc "umsCBServerPort" -minValue 0 -maxValue 65535 umsCtrlPort;
    setParent ..;

    separator -h 20 -w 250;

    text -label "Scene" -font "boldLabelFont";
    separator -h 5 -w 250 -style none;

    rowLayout -numberOfColumns 2;
        text -label "Scale Factor"; floatField -value $scaleFactor -cc "umsCBScaleFactor" umsCtrlScaleFactor;
    setParent ..;
    checkBox -label "Remove Namespace" -value $removeNamespace -cc "umsCBRemoveNamespace" umsCtrlRemoveNamespace;

    checkBox -label "Sync Meshes" -value $syncMeshes -cc "umsCBSyncMeshes" umsCtrlSyncMeshes;
    columnLayout -visible $syncMeshes -columnOffset "left" 20 msuFoldMeshes;
        checkBox -label "Normals" -value $syncNormals -cc "umsCBSyncNormals" umsCtrlSyncNormals;
        checkBox -label "UVs" -value $syncUVs -cc "umsCBSyncUVs" umsCtrlSyncUVs;
        checkBox -label "Colors" -value $syncColors -cc "umsCBSyncColors" umsCtrlSyncColors;
        checkBox -label "Double Sided" -value $makeDoubleSided -cc "umsCBMakeDoubleSided" umsCtrlMakeDoubleSided;
        checkBox -label "Bake Deformers" -value $bakeDeformers -cc "umsCBBakeDeformers" umsCtrlBakeDeformers;
    setParent ..;
    checkBox -label "Sync Blend Shapes" -value $syncBlendShapes -cc "umsCBSyncBlendShapes" umsCtrlSyncBlendShapes;
    checkBox -label "Sync Bones" -value $syncBones -cc "umsCBSyncBones" umsCtrlSyncBones;
    checkBox -label "Sync Textures" -value $syncTextures -cc "umsCBSyncTextures" umsCtrlSyncTextures;

    checkBox -label "Sync Cameras" -value $syncCameras -cc "umsCBSyncCameras" umsCtrlSyncCameras;
    checkBox -label "Sync Lights" -value $syncLights -cc "umsCBSyncLights" umsCtrlSyncLights;

    separator -h 5 -w 250 -style none;
    checkBox -label "Auto Sync" -value $autosync -cc "umsCBAutoSync" umsCtrlAutoSync;
    button -l "Manual Sync" -c "umsCBManualSync";

    separator -h 20 -w 250;

    text -label "Animations" -font "boldLabelFont";
    separator -h 8 -w 250 -style none;

    rowLayout -numberOfColumns 2;
        text -label "Time Scale"; floatField -w 60 -v $animationTS -cc "umsCBAnimationTS" -minValue 0.01 umsCtrlAnimationTS;
    setParent ..;
    rowLayout -numberOfColumns 2;
        text -label "Samples Per Second"; floatField -w 60 -v $animationSPS -cc "umsCBAnimationSPS" -minValue 0.01 umsCtrlAnimationSPS;
    setParent ..;
    checkBox -label "Keyframe Reduction" -value $kfReduction -cc "umsCBKeyframeReduction" umsCtrlKeyframeReduction;
    columnLayout -visible $kfReduction -columnOffset "left" 20 msuFoldKeyframeReduction;
        checkBox -label "Keep Flat Curves" -value $keepFlatCurves -cc "umsCBKeepFlatCurves" umsCtrlKeepFlatCurves;
    setParent ..;

    separator -h 5 -w 250 -style none;

    button -l "Sync" -c "umsCBSyncAnimations";
    
    separator -h 20 -w 250;
    string $pluginVersion = "Plugin Version: " + $version;
    text -label $pluginVersion;

    //    button -l "Import Unity Scene" -w 100 -c "umsCBImport" BUTTON_IMPORT;
    setParent ..;

    showWindow $Window;
}

global proc umsCBServerAddress()
{
    string $v = `textField -q -text umsCtrlAddress`;
    string $ret = `UnityMeshSync_Settings -address $v`;
}

global proc umsCBServerPort()
{
    int $v = `intField -q -value umsCtrlPort`;
    string $ret = `UnityMeshSync_Settings -port $v`;
}

global proc umsCBScaleFactor()
{
    float $v = `floatField -q -value umsCtrlScaleFactor`;
    string $ret = `UnityMeshSync_Settings -scaleFactor $v`;
}

global proc umsCBRemoveNamespace()
{
    float $v = `checkBox -q -value umsCtrlRemoveNamespace`;
    string $ret = `UnityMeshSync_Settings -removeNamespace $v`;
}

global proc umsCBSyncMeshes()
{
    int $v = `checkBox -q -value umsCtrlSyncMeshes`;
    string $ret = `UnityMeshSync_Settings -syncMeshes $v`;
    columnLayout -e -visible $v msuFoldMeshes;
}

global proc umsCBSyncNormals()
{
    int $v = `checkBox -q -value umsCtrlSyncNormals`;
    string $ret = `UnityMeshSync_Settings -syncNormals $v`;
}

global proc umsCBSyncUVs()
{
    int $v = `checkBox -q -value umsCtrlSyncUVs`;
    string $ret = `UnityMeshSync_Settings -syncUVs $v`;
}

global proc umsCBSyncColors()
{
    int $v = `checkBox -q -value umsCtrlSyncColors`;
    string $ret = `UnityMeshSync_Settings -syncColors $v`;
}

global proc umsCBMakeDoubleSided()
{
    int $v = `checkBox -q -value umsCtrlMakeDoubleSided`;
    string $ret = `UnityMeshSync_Settings -makeDoubleSided $v`;
}

global proc umsCBBakeDeformers()
{
    int $v = `checkBox -q -value umsCtrlBakeDeformers`;
    string $ret = `UnityMeshSync_Settings -bakeDeformers $v`;
}

global proc umsCBSyncBlendShapes()
{
    int $v = `checkBox -q -value umsCtrlSyncBlendShapes`;
    string $ret = `UnityMeshSync_Settings -syncBlendShapes $v`;
}

global proc umsCBSyncBones()
{
    int $v = `checkBox -q -value umsCtrlSyncBones`;
    string $ret = `UnityMeshSync_Settings -syncBones $v`;
}

global proc umsCBSyncTextures()
{
    int $v = `checkBox -q -value umsCtrlSyncTextures`;
    string $ret = `UnityMeshSync_Settings -syncTextures $v`;
}

global proc umsCBSyncCameras()
{
    int $v = `checkBox -q -value umsCtrlSyncCameras`;
    string $ret = `UnityMeshSync_Settings -syncCameras $v`;
}

global proc umsCBSyncLights()
{
    int $v = `checkBox -q -value umsCtrlSyncLights`;
    string $ret = `UnityMeshSync_Settings -syncLights $v`;
}

global  proc umsCBAutoSync()
{
    if (`checkBox -q -value umsCtrlAutoSync`) {
        if (`UnityMeshSync_ServerStatus -q -isAvailable`) {
            UnityMeshSync_Settings -autosync 1;
        }
        else {
            print `UnityMeshSync_ServerStatus -q -errorMessage`;
            checkBox -edit -value 0 umsCtrlAutoSync;
        }
    }
    else {
        UnityMeshSync_Settings -autosync 0;
    }
}

global proc umsCBManualSync()
{
    string $ret = `UnityMeshSync_Export`;
}


global proc umsCBAnimationTS()
{
    float $v = `floatField -q -value umsCtrlAnimationTS`;
    string $ret = `UnityMeshSync_Settings -animationTS $v`;
}

global proc umsCBAnimationSPS()
{
    float $v = `floatField -q -value umsCtrlAnimationSPS`;
    string $ret = `UnityMeshSync_Settings -animationSPS $v`;
}

global proc umsCBKeyframeReduction()
{
    int $v = `checkBox -q -value umsCtrlKeyframeReduction`;
    string $ret = `UnityMeshSync_Settings -keyframeReduction $v`;
    columnLayout -e -visible $v msuFoldKeyframeReduction;
}

global proc umsCBKeepFlatCurves()
{
    int $v = `checkBox -q -value umsCtrlKeepFlatCurves`;
    string $ret = `UnityMeshSync_Settings -keepFlatCurves $v`;
}

global proc umsCBSyncAnimations()
{
    string $ret = `UnityMeshSync_Export -target animations`;
}

global proc umsCBImport()
{
    string $ret = `UnityMeshSync_Import`;
}
